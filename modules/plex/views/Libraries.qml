import QtQuick
import Components

// Main Plex home screen: Continue Watching + library list
FocusScope {
    id: browseRoot

    property var navParams: ({})
    property var navListState: navParams.navListState || ({})

    signal navigateTo(string path, var params, var listState)
    // Used for the PIN handoff below: replacing rather than pushing keeps this
    // view from ending up on the stack twice when ProfilePin returns here.
    signal replaceWith(string path, var params)
    signal goBack()

    property var libraries: []
    property string serverName: ""
    property string userName: ""
    // Servers the active user can switch to from the main menu. More than one
    // means the quick-switch action (◄/►) is offered.
    property var switchableServers: []
    property bool canSwitchServer: switchableServers.length > 1
    property string errorMsg: ""

    Connections {
        target: plexBackend

        function onLibrariesLoaded(items) {
            browseRoot.errorMsg = ""
            browseRoot.libraries = items
            if (items.length > 0) {
                var restore = (navListState.currentIndex !== undefined) ? navListState.currentIndex : 0
                libraryList.currentIndex = Math.min(restore, items.length - 1)
                libraryList.positionViewAtIndex(libraryList.currentIndex, ListView.Contain)
            }
        }

        function onErrorOccurred(msg) {
            console.log("[Library] Error: " + msg)
            browseRoot.errorMsg = msg
        }

        // The 401-recovery retry re-runs the user switch, which can turn out to
        // need the profile's PIN.
        function onUserPinRequired(userId, wrongPin) {
            browseRoot.replaceWith("ProfilePin.qml", {
                userId: userId,
                title: browseRoot.userName,
                reauth: true,
                wrongPin: wrongPin
            })
        }
    }

    Component.onCompleted: {
        browseRoot.serverName = plexBackend.get_active_server_name()
        browseRoot.userName = plexBackend.get_active_user_name()
        browseRoot.switchableServers = plexBackend.get_switchable_servers()
        plexBackend.load_libraries()
    }

    function openServerSwitch() {
        if (!canSwitchServer) return
        browseRoot.navigateTo("ServerSelect.qml",
                              { servers: switchableServers, switching: true },
                              { currentIndex: libraryList.currentIndex })
    }

    focus: true

    // ---
    // UI
    // ---

    // Header
    AppBar {
        iconSource: moduleRoot.moduleIcon
        title: moduleRoot.moduleName
        subtitle: browseRoot.serverName + (browseRoot.userName ? " (" + browseRoot.userName + ")" : "")
        anchors.top: parent.top
        anchors.left: parent.left
        anchors.topMargin: root.sh * 0.125 //60
        anchors.leftMargin: root.sw * 0.125 //80
    }

    // Loading Indicator
    Text {
        visible: libraries.length === 0 && browseRoot.errorMsg === ""
        text: "LOADING..."
        color: root.tertiaryColor
        font.family: root.globalFont
        anchors.centerIn: parent
        font.pixelSize: root.sh * 0.05 //24
    }

    // Error message
    Text {
        visible: browseRoot.errorMsg !== ""
        text: browseRoot.errorMsg
        color: root.secondaryColor
        font.family: root.globalFont
        font.capitalization: Font.AllUppercase
        horizontalAlignment: Text.AlignHCenter
        wrapMode: Text.WordWrap
        width: root.sw * 0.6 //384
        anchors.centerIn: parent
        font.pixelSize: root.sh * 0.0375 //18
    }

    // Body
    ListView {
        id: libraryList
        model: libraries
        anchors.top: parent.top
        anchors.left: parent.left
        anchors.topMargin: root.sh * 0.25 //120
        anchors.leftMargin: root.sw * 0.115625 //74
        width: root.sw * 0.76875 //492
        height: root.sh * 0.525 //252
        clip: true
        focus: true

        Keys.onUpPressed: {
            if (count === 0) return
            if (currentIndex > 0) currentIndex--
            else currentIndex = count - 1
            libraryList.positionViewAtIndex(libraryList.currentIndex, ListView.Contain)
        }
        Keys.onDownPressed: {
            if (count === 0) return
            if (currentIndex < count - 1) currentIndex++
            else currentIndex = 0
            libraryList.positionViewAtIndex(libraryList.currentIndex, ListView.Contain)
        }
        Keys.onLeftPressed: browseRoot.openServerSwitch()
        Keys.onRightPressed: browseRoot.openServerSwitch()

        Keys.onReturnPressed: {
            var lib = libraries[currentIndex]
            if (!lib) return

            if (lib.key === "continue_watching") {
                browseRoot.navigateTo("Items.qml", {
                    listType: "continue_watching",
                    title: "CONTINUE WATCHING",
                    libraryName: lib.title
                }, { currentIndex: libraryList.currentIndex })
            } else if (lib.key === "live_tv") {
                browseRoot.navigateTo("LiveChannels.qml", {
                    libraryName: lib.title
                }, { currentIndex: libraryList.currentIndex })
            } else {
                browseRoot.navigateTo("Library.qml", {
                    libraryName: lib.title,
                    sectionId: lib.sectionId,
                    sectionType: lib.sectionType
                }, { currentIndex: libraryList.currentIndex })
            }
        }

        Keys.onPressed: function(event) {
            if (event.key === Qt.Key_Escape || event.key === Qt.Key_Backspace || event.key === Qt.Key_Back) {
                browseRoot.goBack()
                event.accepted = true
            }
        }

        delegate: Item {
            width: libraryList.width
            height: root.sh * 0.0583333 //28

            Item {
                id: textClip
                width: Math.min(rowText.implicitWidth, libraryList.width)
                height: parent.height
                clip: true

                Rectangle {
                    color: root.accentColor
                    anchors.fill: rowText
                    visible: libraryList.currentIndex === index
                }

                Text {
                    id: rowText
                    text: modelData.title || ""
                    color: libraryList.currentIndex === index ? root.surfaceColor : root.primaryColor
                    font.family: root.globalFont
                    font.capitalization: Font.AllUppercase
                    anchors.verticalCenter: parent.verticalCenter
                    x: 0
                    topPadding: root.sh * 0.0041667 //2
                    leftPadding: root.sw * 0.009375 //6
                    rightPadding: root.sw * 0.009375 //6
                    bottomPadding: root.sh * 0.00625 //3
                    font.pixelSize: root.sh * 0.05 //24
                }

                SequentialAnimation {
                    running: (libraryList.currentIndex === index) &&
                             (rowText.implicitWidth > textClip.width)
                    loops: Animation.Infinite
                    onRunningChanged: if (!running) rowText.x = 0
                    PauseAnimation { duration: 1500 }
                    NumberAnimation {
                        target: rowText; property: "x"
                        to: textClip.width - rowText.implicitWidth
                        duration: Math.abs(to) * 20
                    }
                    PauseAnimation { duration: 2000 }
                    PropertyAction { target: rowText; property: "x"; value: 0 }
                }
            }
        }
    }

    // Footer
    HintBar {
        id: footer
        text: root.hints.back + ":BACK " + root.hints.navigate + ":NAVIGATE " + root.hints.select + ":SELECT"
              + (browseRoot.canSwitchServer ? " " + root.hints.change + ":SERVER" : "")
        font.family: root.globalFont
        anchors.bottom: parent.bottom
        anchors.left: parent.left
        anchors.bottomMargin: root.sh * 0.1041667 //50
        anchors.leftMargin: root.sw * 0.125 //80
        font.pixelSize: root.sh * 0.0333333 //16
    }
}
