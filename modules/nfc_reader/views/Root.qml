import QtQuick
import Components

FocusScope {
    id: moduleRoot

    signal goBack()
    // Routes out of this module entirely, to another module's entry point — the
    // app shell handles this one (Main.qml), not the internal Loader below. Named
    // to match the shell's handler, which is why the internal view navigation is
    // navigateToView() rather than navigateTo().
    signal navigateTo(string path, var params, var listState)

    property var navParams: ({})

    property string moduleId: "com.240mp.nfc_reader"
    property var _moduleInfo: appCore ? appCore.get_module_info(moduleId) : ({})
    property string moduleName: _moduleInfo.name || ""
    property string moduleIcon: _moduleInfo.icon || ""

    property var navStack: []
    property var currentParams: ({})

    function navigateToView(viewPath, params, fromState) {
        var resolved = Qt.resolvedUrl(viewPath)
        navStack.push({ source: internalLoader.source, params: currentParams, listState: fromState || {} })
        currentParams = params || {}
        internalLoader.setSource(resolved, { "navParams": params || {} })
    }

    function navigateBack() {
        if (navStack.length === 0) {
            moduleRoot.goBack()
            return
        }
        var prev = navStack.pop()
        if (!prev.source || prev.source.toString() === "") {
            moduleRoot.goBack()
            return
        }
        var restored = Object.assign({}, prev.params)
        restored.navListState = prev.listState || {}
        currentParams = restored
        internalLoader.setSource(prev.source, { "navParams": restored })
    }

    Loader {
        id: internalLoader
        anchors.fill: parent
        focus: true
        onLoaded: { if (item) item.forceActiveFocus() }

        Connections {
            target: internalLoader.item
            ignoreUnknownSignals: true
            function onNavigateTo(path, params, listState) { moduleRoot.navigateToView(path, params, listState) }
            function onGoBack() { moduleRoot.navigateBack() }
        }
    }

    // With no reader driver at all no child view ever loads, so the back keys
    // for the unavailable screen must be handled here.
    Keys.onPressed: function(event) {
        if (!nfcReaderBackend.available &&
            (event.key === Qt.Key_Escape || event.key === Qt.Key_Backspace || event.key === Qt.Key_Back)) {
            moduleRoot.goBack()
            event.accepted = true
        }
    }

    // Shown only on a platform with no reader driver at all. The PN532 USB
    // driver links nothing, so on Linux and macOS this never appears — a
    // build without PC/SC still supports readers, and says so via
    // pcscAvailable in Items.qml.
    Column {
        visible: !nfcReaderBackend.available
        anchors.centerIn: parent
        spacing: root.sh * 0.02

        Text {
            text: "NFC Reader"
            color: root.primaryColor
            font.family: root.globalFont
            font.capitalization: Font.AllUppercase
            font.pixelSize: root.sh * 0.05
            anchors.horizontalCenter: parent.horizontalCenter
        }

        Text {
            text: "Not supported on this platform"
            color: root.secondaryColor
            font.family: root.globalFont
            font.capitalization: Font.AllUppercase
            font.pixelSize: root.sh * 0.033
            anchors.horizontalCenter: parent.horizontalCenter
        }

        Text {
            text: "NFC reader support requires Linux or macOS.\nSee the wiki for details."
            color: root.tertiaryColor
            font.family: root.globalFont
            font.pixelSize: root.sh * 0.025
            horizontalAlignment: Text.AlignHCenter
            anchors.horizontalCenter: parent.horizontalCenter
            lineHeight: 1.4
        }
    }

    HintBar {
        visible: !nfcReaderBackend.available
        text: root.hints.back + ":BACK"
        anchors.bottom: parent.bottom
        anchors.left: parent.left
        anchors.bottomMargin: root.sh * 0.1041667
        anchors.leftMargin: root.sw * 0.125
    }

    Component.onCompleted: {
        if (nfcReaderBackend.available) {
            nfcReaderBackend.setModuleActive(true)
            nfcReaderBackend.reloadMapping()
            navigateToView("Items.qml", {})
        }
    }

    // Card taps must do nothing once the user leaves the module.
    Component.onDestruction: nfcReaderBackend.setModuleActive(false)
}
